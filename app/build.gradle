buildscript {
    ext {
        kotlinVersion = '1.2.41'
        log4jVersion = "2.11.0"
        springBootVersion = "2.0.2.RELEASE"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'idea'


    repositories {
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        // Kotlin
        compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion")
        compile("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
        compile("org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion")

        // Logging
        compile("org.apache.logging.log4j:log4j-api:$log4jVersion")
        compile("org.apache.logging.log4j:log4j-core:$log4jVersion")

        // JSON
        compile('org.json:json:20180130')

        // testing
        testCompile('io.kotlintest:kotlintest:2.0.5')
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

project(':compiler') {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        compile 'org.jparsec:jparsec:3.0'
        compile project(':common')
    }

    bootJar {
        baseName = 'rulesapp-compiler'
        version =  '0.1.0'
    }

    task docker(type: Exec, dependsOn: bootJar) {
        commandLine = "docker"
        args = ["build", "-t", bootJar.baseName, "-f", "${projectDir}/Dockerfile", "."]
    }
}

project(':engine') {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        jcenter()
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
        compile("com.github.kittinunf.fuel:fuel:1.13.0")
        compile project(':common')
    }

    bootJar {
        baseName = 'rulesapp-engine'
        version =  '0.1.0'
    }

    task docker(type: Exec, dependsOn: bootJar) {
        commandLine = "docker"
        args = ["build", "-t", bootJar.baseName, "-f", "${projectDir}/Dockerfile", "."]
    }
}